plugins
{
	id 'groovy'
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
}

repositories
{
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven { url "https://cursemaven.com/" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}

sourceSets
{
	main
	{
		java { srcDirs = ['src/main/java'] }
		groovy { srcDirs = ['src/main/groovy'] }
		resources { srcDirs = ['src/main/resources'] }
	}
}

dependencies
{
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	// Fabric API
	// modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	// Don't forget to declare | "fabric":"*" | as a dependency in fabric.mod.json
	
	// Mod Menu
	// modApi "com.terraformersmc:modmenu:${project.modmenu_version}"
	
	// Roughly Enough Items
	// modApi "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	
	// Architectury API
	modApi "dev.architectury:architectury-fabric:${project.architectury_version}"
	// ^ Note: TCDCommons API may depend on Architectury!
	
	// Spark, for performance profiling reasons
	// modImplementation "curse.maven:spark-361579:4950610" //This file is for 1.20.4
	
	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.
	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
	
	// All .jar files in 'src/main/resources/META-INF/jarjar'
	modImplementation fileTree(dir: 'src/main/resources/META-INF/jarjar', include: '*.jar')
}

base { archivesName = "${project.mod_id}-${project.mod_version}" }

ext { fmj_jars = null; fmj_mixins = null; }
processResources
{
	def rssDirPath = new File("${project.projectDir}/src/main/resources").toPath();
	
	//obtain all jar files in `jarjar`, and construct a String for the `jars` entry in `fabric.mod.json`.
	//due to a bug in the compiler, this uses a cheesy workaround that injects json code into a json string
	def fmj_jars = new ArrayList<String>();
	fileTree(dir: rssDirPath.resolve("/src/main/resources/META-INF/jarjar").toString(), include: '*.jar')
		.each { jarFile -> fmj_jars.add(rssDirPath.relativize(jarFile.toPath()).toString().replace('\\', '/')) };
	fmj_jars = fmj_jars.collect { jarFile -> "{ \"file\": \"${jarFile}\" }" };
	fmj_jars = "\", \"jars\": [" + fmj_jars.join(', ') + "], \"\": \"";
	project.ext.fmj_jars = fmj_jars;
	
	//obtain all mixin files in `resources` and construct a String for the `mixins` entry in `fabric.mod.json`
	//due to a bug in the compiler, this uses a cheesy workaround that injects json code into a json string
	def fmj_mixins = new ArrayList<String>();
	fileTree(dir: rssDirPath.toString(), include: '*.json').each { jsonFile ->
		//skip non-mixin files
		if(!jsonFile.getName().contains(".mixin.")) return;
		//add file name to the list
		fmj_mixins.add(jsonFile.getName());
	}
	fmj_mixins = fmj_mixins.collect { jsonFile -> "\"${jsonFile}\"" }.join(", ");
	fmj_mixins = "\", \"mixins\": [" + fmj_mixins + "], \"\": \"";
	project.ext.fmj_mixins = fmj_mixins;
	
	//expand project properties to resources that might need it
	filesMatching(["**/*.json", "pack.mcmeta"]) { expand(project: project); }
}

tasks.withType(JavaCompile).configureEach
{
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java
{
	// Loom will automatically attach sourcesJar to a RemapSourcesJar
	// task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
	
	// Generate Java docs.
	javadoc.options.addStringOption('Xdoclint:none', '-quiet')
	javadoc.options.encoding = 'UTF-8';
	withJavadocJar()
	
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar
{
	//exclude all .class files outside of the maven group
	def includeOnlyPackage = project.maven_group.replaceAll('\\.', '/')
	eachFile
	{ fileCopyDetails ->
        if (fileCopyDetails.name.endsWith('.class') && !fileCopyDetails.relativePath.pathString.startsWith(includeOnlyPackage)) {
            fileCopyDetails.exclude()
        }
    }
    
    //include the license file
	from("LICENSE")
	
	//put gradle project properties into manifest properties as well.
	//this will allow the mod to read its own gradle properties during runtime
	project.properties.each
	{ key, value ->
		//skip certain entries
		if(!(value instanceof String)) return;
		
		//sanitize keys so they adhere to the MANIFEST spec, and assign attributes
		key = key.replace('.', '_');
		manifest { attributes(("project-" + key): ("" + value)) }
	}
}